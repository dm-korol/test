plugins {
    id 'java'
    id 'org.springframework.boot' version '2.3.7.RELEASE'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.dps.EntryPoint'
        )
    }
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}
sourceSets {
    testIntegration {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/test-integration/java')
        }
        resources.srcDir file('src/test-integration/resources')
    }
}

configurations {
    testIntegrationImplementation.extendsFrom testImplementation
    testIntegrationRuntime.extendsFrom testRuntime
}

dependencies {
    // Spring
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.3.7.RELEASE'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '2.3.7.RELEASE'

    // OpenAPI documentation
    implementation("org.springdoc:springdoc-openapi-ui:1.4.6")
    // Database
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.18'

    // Lombok
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.16'
    annotationProcessor('org.projectlombok:lombok:1.18.16')

    //Integration Tests
    testIntegrationImplementation 'io.rest-assured:rest-assured:4.3.3'
    testIntegrationImplementation group: 'org.testng', name: 'testng', version: '7.4.0'

    // Unit Tests
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    //JSON
    implementation group: 'org.json', name: 'json', version: '20090211'

}

test {
    useJUnitPlatform()
}

task testIntegration(type: Test) {
    testClassesDirs = sourceSets.testIntegration.output.classesDirs
    classpath = sourceSets.testIntegration.runtimeClasspath
    check.dependsOn testIntegration

    useTestNG() { //Tells Gradle to use TestNG
        useDefaultListeners = true // Tells TestNG to execute its default reporting structure
    }

    //we want display the following test events
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
    }
}